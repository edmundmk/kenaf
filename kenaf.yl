

expr_atom ::= LPN expr RPN .
expr_atom ::= NULL .
expr_atom ::= FALSE .
expr_atom ::= TRUE .
expr_atom ::= NUMBER .
expr_atom ::= STRING .
expr_atom ::= IDENTIFIER .
expr_atom ::= expr_atom PERIOD IDENTIFIER .
expr_atom ::= expr_atom LSQ expr RSQ .
expr_atom ::= expr_atom LPN unpack_list RPN .

expr_unary ::= expr_atom .
expr_unary ::= MINUS expr_unary .
expr_unary ::= PLUS expr_unary .
expr_unary ::= TILDE expr_unary .

expr_muldiv ::= expr_unary .
expr_muldiv ::= expr_muldiv ASTERISK expr_unary .
expr_muldiv ::= expr_muldiv SOLIDUS expr_unary .
expr_muldiv ::= expr_muldiv TILDE expr_unary .
expr_muldiv ::= expr_muldiv PERCENT expr_unary .

expr_addsub ::= expr_muldiv .
expr_addsub ::= expr_addsub PLUS expr_muldiv .
expr_addsub ::= expr_addsub MINUS expr_muldiv .
expr_addsub ::= expr_addsub CONCAT expr_muldiv .

expr_shift ::= expr_addsub .
expr_shift ::= expr_shift LSHIFT expr_addsub .
expr_shift ::= expr_shift RSHIFT expr_addsub .
expr_shift ::= expr_shift ASHIFT expr_addsub .

expr_bitand ::= expr_shift .
expr_bitand ::= expr_bitand AMPERSAND expr_shift .

expr_bitxor ::= expr_bitand .
expr_bitxor ::= expr_bitxor CARET expr_bitand .

expr_bitor ::= expr_bitxor .
expr_bitor ::= expr_bitor VBAR expr_bitxor .

expr_compare ::= expr_bitor .
expr_compare ::= expr_compare op_compare expr_bitor .

op_compare ::= EQ .
op_compare ::= NE .
op_compare ::= LT .
op_compare ::= LE .
op_compare ::= GT .
op_compare ::= GE .
op_compare ::= IS .
op_compare ::= IS NOT .

expr_not ::= expr_compare .
expr_not ::= NOT expr_not .

expr_and ::= expr_not .
expr_and ::= expr_and AND expr_not .

expr_or ::= expr_and .
expr_or ::= expr_or OR expr_and .

expr ::= expr_or .
expr ::= constructor .




constructor ::= DEF LPN parameters RPN block END .
constructor ::= DEF LPN parameters RPN COLON expr .
constructor ::= DEF prototype object_keys END .
constructor ::= LSQ unpack_list RSQ .
constructor ::= LBR keyval_list RBR .

prototype ::= .
prototype ::= IS expr .

