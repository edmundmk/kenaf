--
--  spectral-norm.kf
--  Based on code from the Computer Language Benchmarks Game
--  https://salsa.debian.org/benchmarksgame-team/benchmarksgame/
--

def A( i, j )
    var ij = i + j
    return 1.0 / ( ( ij + 1 ) * ij * 0.5 + i + 1 )
end

def multiplyAv( n, u, v )
    for i = 0 : n do
        var x = 0.0
        for j = 0 : n do x += u[ j ] * A( i, j ) end
        v[ i ] = x
    end
end

def multiplyAtv( n, u, v )
    for i = 0 : n do
        var x = 0.0
        for j = 0 : n do x += u[ j ] * A( j, i ) end
        v[ i ] = x
    end
end

def multiplyAtAv( n, u, v, x )
    multiplyAv( n, u, x )
    multiplyAtv( n, x, v )
end

var _, n = args ...
n = if n then number( n ) else 100 end

var u, v, x = [], [], []
u.resize( n )
v.resize( n )
x.resize( n )

-- Create unit vector.
for i = 0 : n do u[ i ] = 1.0 end

-- 20 steps of the power method.
for i = 0 : 10 do
    multiplyAtAv( n, u, v, x )
    multiplyAtAv( n, v, u, x )
end

-- B=AtA A multiplied by A transposed
-- v.Bv /(v.v) eigenvalue of v
var vBv, vv = 0, 0
for i = 0 : n do
    vBv += u[ i ] * v[ i ]
    vv  += v[ i ] * v[ i ]
end

print( "%.9f\n", sqrt( vBv / vv ) )

